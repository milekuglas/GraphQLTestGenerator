Test:
    package=Package
    class_name=Class
    scenario=Scenario
    'request' ':'
    request=Request
    cases*=Case
;

Package:
    'package' ':' name=/[a-z][a-z0-9_]*((\.[a-z0-9_]+)+[0-9a-z_])?/
;

Class:
    'class' ':' name=ID
;

Scenario:
    'scenario' ':' name=/[A-Za-z0-9].*/
;

Case:
    'response' ':'
    response=Response
    examples=Examples
;

Request:
    '"""'
    type=RequestType name=ID '{'
    attributes*=Attribute
    '}'
    '"""'
;

RequestType:
    name='query' | 'mutation'
;

RequestBodyWithArgs:
    name=ID ('(' arguments+=Argument[','] ')')? '{'
    attributes*=Attribute
    '}'
;

RequestBody:
    name=ID (body_attributes=BodyAttributes)?
;

BodyAttributes:
    '{'
    attributes*=Attribute
    '}'
;

Attribute:
    name=ID('(' arguments+=Argument[','] ')')? ('{' attributes*=Attribute '}')?
;

Argument:
    name=ID ':' ArgumentValue
;

ArgumentValue:
    SimpleValue | CompoundValue | TagValue
;

SimpleValue:
    STRING | INT | FLOAT | BOOL
;

CompoundValue:
    '{' (arguments*=Argument[','])? '}'
;

TagValue:
    '<' name=ID '>'
;

SpecialTag:
    '*' name=ID
;

Response:
    '"""'
   (JsonObject | JsonArray)
   '"""'
;

JsonObject:
	'{'
		members*=JsonMember[',']
	'}'
;

JsonMember:
	key=STRING ':' value=JsonValueWithTags
;

JsonValue:
	JsonObject  | JsonArray | JsonNull | STRING | INT | FLOAT | BOOL
;

JsonValueWithTags:
    JsonValue | TagValue | SpecialTag
;

JsonArray:
	'[' values*=JsonValueWithTags[','] ']'
;

JsonNull:
  'null'
;

Examples:
    'examples' ':'
    RowSeparator
    attributes=ExampleAttributes
    RowSeparator
    examples*=Example
;

RowSeparator:
    ('=')+
;

ExampleAttributes:
    '||' attributes*=ExampleAttribute['||'] '||'
;

ExampleAttribute:
    ('*')? name=ID
;

JsonValueWithEmptyValue:
    JsonValue | '*'
;

Example:
    ('||' values*=JsonValueWithEmptyValue['||'] '||')*
    RowSeparator
    should?=STRING
    request_generated?=STRING
    response_generated?=/(?ms)\"{3}(.*?)\"{3}/
;

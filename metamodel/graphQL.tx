Test:
    package=Package
    class_name=Class
    scenario=Scenario
    /(#{1,6} )?request/ ':'
    request=Request
    cases*=Case
;

Package:
    /(#{1,6} )?package/ ':' name=/[a-z][a-z0-9_]*((\.[a-z0-9_]+)+[0-9a-z_])?/
;

Class:
    /(#{1,6} )?class/ ':' name=ID
;

Scenario:
    /(#{1,6} )?scenario/ ':' name=/[A-Za-z0-9].*/
;

Case:
    /(#{1,6} )?response/ ':'
    response=Response
    examples=Examples
;

Request:
    '```graphql'
    type=RequestType name=ID '{'
    attributes*=Attribute
    '}'
    '```'
;

RequestType:
    'query' | 'mutation'
;

Attribute:
    name=ID('(' arguments+=Argument[','] ')')? ('{' attributes*=Attribute '}')?
;

Argument:
    name=ID ':' value=ArgumentValue
;

ArgumentValue:
    SimpleValue | CompoundValue | TagValue
;

SimpleValue:
    STRING | INT | FLOAT | BOOL
;

CompoundValue:
    '{' (arguments*=Argument[','])? '}'
;

TagValue:
    '<' name=ID '>'
;

Response:
    '```json'
    json=JsonObject
    '```'
;

JsonObject:
	'{'
		members*=JsonMember[',']
	'}'
;

JsonMember:
	key=STRING ':' value=JsonValueWithTags
;

JsonValue:
	JsonObject  | JsonArray | JsonNull | STRING | INT | FLOAT | BOOL
;

JsonValueWithTags:
    JsonValue | TagValue
;

MultiplierValue:
    INT | TagValue
;

Multiplier:
    '[' value = MultiplierValue ']'
;

JsonArray:
	'[' (multiplier=Multiplier)? values*=JsonValueWithTags[','] ']'
;

JsonNull:
  value='null'
;

Examples:
    /(#{1,6} )?examples/ ':'
    attributes=ExampleAttributes
    examples*=Example
;

RowSeparator:
    '| ' (('-')+ ' |')+ 
;

ExampleAttributes:
    '|' attributes*=ExampleAttribute['|'] '|'
;

ExampleAttribute:
    name=ID
;

JsonValueWithEmptyValue:
    JsonValue | '*'
;

Example:
    RowSeparator
    ('|' values*=JsonValueWithEmptyValue['|'] '|')*
;
